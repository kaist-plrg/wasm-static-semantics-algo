open Runner

let version = "0.1"

(* Commands *)

let elab_command =
  Core.Command.basic ~summary:"parse and elaborate a spec"
    (let open Core.Command.Let_syntax in
     let open Core.Command.Param in
     let%map filenames = anon (sequence ("spec files" %: string)) in
     fun () ->
       let elaborate_result =
         let* spec = parse_spec_files filenames in
         let* spec_il = elaborate spec in
         Ok spec_il
       in
       match elaborate_result with
       | Ok spec_il ->
           Format.printf "%s\n" (Lang.Il.Print.string_of_spec spec_il)
       | Error e -> Format.printf "%s\n" (Runner.Error.string_of_error e))

let structure_command =
  Core.Command.basic ~summary:"structure a spec"
    (let open Core.Command.Let_syntax in
     let open Core.Command.Param in
     let%map filenames = anon (sequence ("spec files" %: string)) in
     fun () ->
       let structure_result =
         let* spec = parse_spec_files filenames in
         let* spec_il = elaborate spec in
         let spec_sl = structure spec_il in
         Ok spec_sl
       in
       match structure_result with
       | Ok spec_sl ->
           Format.printf "%s\n" (Lang.Sl.Print.string_of_spec spec_sl)
       | Error e -> Format.printf "%s\n" (Runner.Error.string_of_error e))

let p4parse_command =
  Core.Command.basic ~summary:"parse a P4 program"
    (let open Core.Command.Let_syntax in
     let open Core.Command.Param in
     let%map filenames = anon (sequence ("spec files" %: string))
     and includes_target = flag "-i" (listed string) ~doc:"p4 include paths"
     and filename_target = flag "-p" (required string) ~doc:"p4 file to parse"
     and roundtrip =
       flag "-r" no_arg ~doc:"perform a round-trip parse/unparse"
     in
     fun () ->
       let do_roundtrip () =
         let* rountrip_result =
           Runner.parse_p4_file_with_roundtrip roundtrip filenames
             includes_target filename_target
         in
         Ok rountrip_result
       in
       match (roundtrip, Runner.Handlers.il do_roundtrip) with
       | false, Ok unparsed_string ->
           Format.printf "Parse succeeded:\n%s\n" unparsed_string
       | true, Ok unparsed_string ->
           Format.printf "Roundtrip succeeded:\n%s\n" unparsed_string
       | false, Error e ->
           Format.printf "Parse failed:\n  %s\n"
             (Runner.Error.string_of_error e)
       | true, Error e ->
           Format.printf "Roundtrip failed:\n  %s\n"
             (Runner.Error.string_of_error e))

let wasm_parse_command =
  Core.Command.basic ~summary:"parse a Wasm program"
    (let open Core.Command.Let_syntax in
     let open Core.Command.Param in
     let%map filename = flag "-p" (required string) ~doc:"wasm file to parse" in
     fun () ->
       let do_parse () =
         let filenames_spec = Cli.Command.collect_spec_files Targets_wasm.Wasm.Target.spec_dir in
         let* spec = Runner.parse_spec_files filenames_spec in
         let* spec_il = Runner.elaborate spec in
         let input =
           Targets_wasm.Wasm.Typecheck.make ~filename () in
         let* (_, values) =
           Targets_wasm.Wasm.Typecheck.parse ~spec:spec_il input
         in
           Ok (spec_il, values)
      in
      match Runner.Handlers.il do_parse with
      | Ok (spec_il, values) ->
        Format.printf "Parse succeeded:\n";
        List.iter (fun v ->
            Format.printf "%a\n" (Concrete.Pp.pp_program spec_il) v) values;
      | _ ->
         Format.printf "Parse failed\n")


(* Instantiate CLI commands for P4 *)
module P4_Cmd = Cli.Command.Make (Targets_p4.P4.Target)

(* Instantiate CLI commands for Wasm *)
module Wasm_Cmd = Cli.Command.Make (Targets_wasm.Wasm.Target)

let p4_command =
  let tasks = [ P4_Cmd.Pack (module Targets_p4.P4.Typecheck) ] in
  Core.Command.group ~summary:"P4 commands"
    [
      ("typecheck", P4_.P4.command); ("coverage", P4_Cmd.make_coverage tasks);
    ]

let wasm_command =
  let tasks = [ Wasm_Cmd.Pack (module Targets_wasm.Wasm.Typecheck) ] in
  Core.Command.group ~summary:"Wasm commands"
    [
      ("typecheck", Wasm_.Wasm.command); ("coverage", Wasm_Cmd.make_coverage tasks);
    ]

let command =
  Core.Command.group ~summary:"SpecTec command line tools"
    [
      ("elab", elab_command);
      ("struct", structure_command);
      ("p4parse", p4parse_command);
      ("p4", p4_command);
      ("wasmparse", wasm_parse_command);
      ("wasm", wasm_command);
    ]

let () = Command_unix.run ~version command


